import org.apache.tools.ant.filters.*

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:grgit:2.1.0'
        classpath 'org.ajoberstar.reckon:reckon-gradle:0.3.0'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

ext {
    versionPattern = /\d+\.\d+\.\d+/
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: "org.ajoberstar.grgit"
apply plugin: "org.ajoberstar.reckon"
apply plugin: 'com.bmuschko.nexus'

group = "cl.daplay"
description = "Surbtc made easy"

mainClassName = 'cl.daplay.sur.Main'

reckon {
    normal = scopeFromProp()
    preRelease = snapshotFromProp()
}

task copyScripts(type: Copy) {
    description = "Copy scripts from src/main/bash to release dir /bin"

    from 'src/main/bash'
    into 'build/scripts'
    include '**/*'

    filter(ReplaceTokens, tokens: [ SUR_VERSION: project.version.toString() ])
}

task updateInstallScript {
    description = 'Updates version in install.sh'

    if (!requireReleaseVersion()) {
        doLast {
            println "task ${this.name}, won't be executed, as $version is an SNAPSHOT version"
        }

        return
    }

    // replace text in README.md
    doLast {
        def file = new File(project.projectDir, "install.sh");
        def text = file.text;
        file.text = text.replaceAll versionPattern, version.toString()
    }

    // add, commit and push change
    doLast {
        grgit.add(patterns: ['install.sh'])
        grgit.commit(message: "chore(install.sh) version change: '${version.toString()}'.")
        grgit.push()
    }
}

startScripts {
    unixStartScriptGenerator.template = resources.text.fromFile('buildSrc/src/main/resources/cl/daplay/sur/unixStartScript.txt')
}

startScripts.finalizedBy(copyScripts)

task generateVersionFile {
    description = 'Generates file "version.properties".'

    doLast {
        def out = new File(project.buildDir, "resources/main/version.properties")

        def props = new Properties()
        props.setProperty('version', project.version.toString())

        props.store(out.newWriter(), null)
    }
}
processResources.finalizedBy generateVersionFile

repositories {
    mavenCentral()
}

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

dependencies {
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.13'
    compile group: 'cl.daplay', name: 'jsurbtc', version: '2.1.0'

    testCompile 'junit:junit:4.12'
}

def requireReleaseVersion() {
    return !version.toString().contains("SNAPSHOT") && (version.toString() ==~ versionPattern)
}

// unused tasks by plugin Reckon
project.tasks.remove(reckonTagCreate)
project.tasks.remove(reckonTagPush)

task release() {
    if (!requireReleaseVersion()) {
        doLast {
            println "task Release, won't be executed, as $version is an SNAPSHOT version"
        }

        return
    }

    final String releaseName = "v${version}"

    doLast {
        println "releasing: $releaseName"
        grgit.tag.add(name: releaseName, message: releaseName, annotate: true)
        grgit.push(refsOrSpecs: ["refs/tags/${releaseName}"])
    }
}

release.dependsOn build
release.finalizedBy updateInstallScript
release.finalizedBy uploadArchives

modifyPom {
    project {

        name project.name
        description project.description
        url 'https://github.com/daplay/sur'

        scm {
            url 'https://github.com/daplay/sur'
            connection 'scm:https://github.com/daplay/sur.git'
            developerConnection 'scm:git://github.com/daplay/sur.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'daplay'
                name 'Daniel Meneses'
                email 'daplay@gmail.com'
            }
        }
    }
}

